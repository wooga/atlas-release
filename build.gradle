/*
 * Copyright 2017 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'nebula.release' version '5.0.0'
    id 'jacoco'
    id "com.github.kt3k.coveralls" version "2.8.1"
}

group 'net.wooga.gradle'
description = 'Unity3D plugin for Gradle.'

sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/groovy')
        }
        resources.srcDir 'src/integrationTest/resources'
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

idea.module {
    testSourceDirs += file('src/integrationTest/groovy')
    scopes.TEST.plus += [configurations.integrationTestCompile]
}

pluginBundle {
    website = 'https://github.com/wooga/atlas-release'
    vcsUrl = 'https://github.com/wooga/atlas-release'
    tags = ['Wooga', 'release']

    plugins {
        release {
            id = 'net.wooga.release'
            displayName = 'Gradle Wooga Release plugin'
            description = 'Plugin for wooga unity library releases'
        }
    }
}

repositories {
    jcenter()

    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }

    testCompile 'com.netflix.nebula:nebula-test:latest.release'
    testCompile('org.jfrog.artifactory.client:artifactory-java-client-services:+') {
        exclude module: 'logback-classic'
    }

    compile 'com.netflix.nebula:nebula-release-plugin:5.+'
    compile 'org.ajoberstar:gradle-git:1.7.+'
    compile 'cz.malohlava:visteg:1.0.+'
    compile 'gradle.plugin.net.wooga.gradle:atlas-paket:0.6.0'
    compile 'gradle.plugin.net.wooga.gradle:atlas-github:0.3.0'

    compile gradleApi()
    compile localGroovy()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    executionData(test)
    executionData(integrationTest)
}

tasks.coveralls {
    onlyIf {System.getenv("CI") && JavaVersion.current().isJava8Compatible()}
}

project.tasks.releaseCheck.dependsOn project.tasks.check
project.tasks.release.dependsOn project.tasks.assemble

project.tasks.final.dependsOn project.tasks.publishPlugins
project.tasks.publishPlugins.mustRunAfter project.tasks.postRelease

project.tasks.snapshot.dependsOn project.tasks.publishToMavenLocal

project.tasks.publishToMavenLocal.mustRunAfter project.tasks.postRelease